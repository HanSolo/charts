import java.text.SimpleDateFormat

plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.google.osdetector'           version '1.7.0'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id 'org.beryx.jlink'                 version '2.24.4'
    id 'net.nemerosa.versioning'         version '2.15.1'
    id 'biz.aQute.bnd.builder'           version '6.1.0'
}

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'maven-publish'
apply plugin: 'signing'

description   = 'Charts is a JavaFX library containing different kind of charts'
mainClassName = "$moduleName/eu.hansolo.fx.charts.Demo"

Date buildTimeAndDate = new Date()
ext {
    buildDate       = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime       = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    platform        = osdetector.os == 'osx' ? osdetector.arch == 'aarch_64' ? 'mac-aarch64' : 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os == 'linux' ? osdetector.arch == 'aarch_64' ? 'linux-aarch64' : 'linux' : osdetector.os
    ciOssrhUsername = System.getenv('OSSRH_USERNAME')
    ciOssrhPassword = System.getenv('OSSRH_PASSWORD')
    ciGHUser        = System.getenv('GH_USER')
    ciGHToken       = System.getenv('GH_TOKEN')
    gpgkey          = System.getenv("GPG_PRIVATE_KEY")
    gpgpassphrase   = System.getenv("PASSPHRASE")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-swing:${javafxVersion}:$platform"
    implementation "eu.hansolo:jdktools:17.0.2"
    implementation "eu.hansolo:toolbox:17.0.11"
    implementation "eu.hansolo:toolboxfx:17.0.21"
    implementation "eu.hansolo.fx:heatmap:17.0.3"
    implementation "eu.hansolo.fx:countries:17.0.16"
}

jar {
    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'eu.hansolo.fx.charts',
                'Export-Package'        : 'eu.hansolo.fx.charts, eu.hansolo.fx.charts.data, eu.hansolo.fx.charts.event, eu.hansolo.fx.charts.series, eu.hansolo.fx.charts.tools, eu.hansolo.fx.charts.voronoi, eu.hansolo.fx.geometry, eu.hansolo.fx.geometry.tools, eu.hansolo.fx.geometry.transform'
        )
    }
}


// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    if (gpgkey && gpgpassphrase) {
        useInMemoryPgpKeys(gpgkey, gpgpassphrase)
    }

    sign configurations.archives
    sign publishing.publications
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            group      = 'eu.hansolo.fx'
            artifactId = 'charts'
            version    = "${version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().dependencies.'*'.findAll {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.remove(it.classifier)
                }
            }

            pom {
                name        = 'charts'
                description = 'A JavaFX library for scientific charts'
                url         = "https://github.com/HanSolo/charts/wiki"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url  = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id    = "HanSolo"
                        name  = "Gerrit Grunwald"
                        email = "han.solo@mac.com"
                    }
                }
                scm {
                    connection          = "scm:git:https://HanSolo/charts"
                    developerConnection = "scm:git:https://HanSolo/charts"
                    url                 = "https://github.com/HanSolo/charts"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url  = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = null == ciOssrhUsername ? ossrhUsername : ciOssrhUsername
                password = null == ciOssrhPassword ? ossrhPassword : ciOssrhPassword
            }
        }
        maven {
            name = "GitHubPackages"
            url  = "https://maven.pkg.github.com/HanSolo/charts"
            credentials {
                username = null == ciGHUser ? ghUser : ciGHUser
                password = null == ciGHToken ? ghToken : ciGHToken
            }
        }
    }
}
